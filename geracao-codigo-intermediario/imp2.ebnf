@@grammar::IMP
@@eol_comments :: /#.*?$/

@@keyword :: 'and'
@@keyword :: 'not'
@@keyword :: 'or'
@@keyword :: 'True'
@@keyword :: 'False'
@@keyword :: '+'
@@keyword :: '-'
@@keyword :: '(' ')'
@@keyword :: '=='
@@keyword :: '<'
@@keyword :: '<='
@@keyword :: '>'
@@keyword :: '>='
@@keyword :: ':='
@@keyword :: 'while' 
@@keyword :: 'if' 'then' 'else' 
@@keyword :: 'var' '='
@@keyword :: 'const' '='
@@keyword :: ';'
@@keyword :: 'skip'
@@keyword :: '{' '}'
@@keyword :: 'print'

start = [ds:dec_seq] [cs:cmd_seq] $ ;

dec_seq = { d:dec }* ;

dec = const | var | fn | rec ;

const = op:"const" idn:identifier "=" e:exp ";" ;

# This makes sure that an identifier does not match a keyword.
@name
identifier = /(?!\d)\w+/ ;

exp = paren_exp | bin_exp | un_exp | @:atom ;

paren_exp = "(" e:exp ")" ;

bin_exp = e1:exp op:binop e2:exp ;

binop = "and"| "or" | "==" | "<=" | ">=" | "<" | ">"  | "+" | "-" | "*" | "/"  ; 

un_exp = op:"not" e:exp ;

atom = @:num_atom  | @:truth_atom | @:id_atom ;

num_atom = number ;

number = /\d+/ ;

truth_atom = truth ;

truth = 'True' | 'False' ;

id_atom = identifier ;

var = op:"var" ","%{ idn:identifier "=" e:exp }+ ";" ;

fn = op:"def" idn:identifier '(' f:formal ')' b:blk ;

formal = ','%{ identifier }* ;

blk = "{" ds:dec_seq cs:cmd_seq "}" ;

rec = op:"rec" idn:identifier '(' f:formal ')' b:blk ;

cmd_seq = ";"%{ac:atom_cmd}+ ;

atom_cmd = print | cond | loop | assign | call | skip | ret ;

cond = op:"if" t:paren_exp b1:blk ["else" b2:blk] ; 

loop = op:"while" t:paren_exp b:blk ;

assign = idn:identifier op:":=" e:exp ;

print = op:"print" e:exp ;

call = idn:identifier '(' a:actual ')' ;

actual = ','%{ exp }* ;

skip = "skip" ;

ret = "return" e:exp ;

